
enum BaseType {
    Char
    I32
    F32
    Bool
    U8
    Void

    Pointer
    Structure
    Function
    Method
    Array
}

def BaseType::str(this): string {
    return match this {
        Char => "char"
        I32 => "i32"
        F32 => "f32"
        Bool => "bool"
        U8 => "u8"
        Void => "void"
        Pointer => "&"
        Structure => "structure"
        Function => "function"
        Method => "method"
        Array => "[]"
    }
}

struct Type {
    base: BaseType
    ptr: &Type
    name: string
    span: Span
    size_expr: &AST

    struct_def: &Structure

    func_def: &Function
    return_type: &Type
    params: &Vector      // Vector<&Type>
}

def Type::new(base: BaseType, span: Span): &Type {
    let type = calloc(1, sizeof(Type)) as &Type
    type.base = base
    type.span = span
    return type
}

def Type::new_link(base: BaseType, next: &Type, span: Span): &Type {
    let type = Type::new(base, span)
    type.ptr = next

    // This is a hack to attach methods to strings
    if base == BaseType::Pointer and next? and next.base == BaseType::Char {
        type.name = "string"
    }
    return type
}

def Type::ptr_to(base: BaseType, span: Span): &Type {
    let next = Type::new(base, span)
    return Type::new_link(BaseType::Pointer, next, span)
}

def Type::is_struct_or_ptr(&this): bool {
    if .base == BaseType::Structure return true
    if .base != BaseType::Pointer return false
    return .ptr.base == BaseType::Structure
}

def Type::is_numeric(&this): bool {
    return .base == BaseType::I32 or .base == BaseType::F32
}

def Type::eq(&this, other: &Type): bool {
    if (this == null and other == null) return true
    if (this == null or other == null) return false

    if .base != other.base return false
    if .base == BaseType::Method return false

    if .base == BaseType::Function {
        if not .return_type.eq(other.return_type) return false
        if .params.size != other.params.size return false
        for let i = 0; i < .params.size; i += 1 {
            let a = .params.at(i) as &Type
            let b = other.params.at(i) as &Type
            if not a.eq(b) return false
        }
        return true
    }

    if .base == BaseType::Structure return .name.eq(other.name)
    if .base == BaseType::Pointer {
        // EXPERIMENTAL: `untyped_ptr` is equivalent to any pointer type.
        if .ptr.base == BaseType::Void or other.ptr.base == BaseType::Void {
            return true
        }
        return .ptr.eq(other.ptr)
    }
    return true
}

def Type::str(&this): string {
    return match .base {
        Void => "void"
        Char => "char"
        I32 => "i32"
        F32 => "f32"
        Bool => "bool"
        U8 => "u8"
        Pointer => `&{.ptr.str()}`
        Array => `[{.ptr.str()}]`
        Structure => .name
        Function => "<function>"
        Method => "<method>"
    }
}

def Type::reverse(&this): &Type {
    let rev = null as &Type
    let cur = this
    while cur? {
        let tmp = cur.ptr
        cur.ptr = rev
        rev = cur
        cur = tmp
    }
    return rev
}

def Type::is_string(&this): bool {
    return .base == BaseType::Pointer and .ptr.base == BaseType::Char
}

def Type::decay_array(&this): &Type {
    if .base != BaseType::Array return this
    return Type::new_link(BaseType::Pointer, .ptr, .span)
}
